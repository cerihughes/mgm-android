def getGitRevision = { ->
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            standardOutput = stdout
            commandLine 'git', 'rev-list', '--first-parent', '--count', 'HEAD'
        }
        println("Building revision #" + stdout)
        return stdout.toString("ASCII").trim() as Integer
    }
    catch (Exception e) {
        e.printStackTrace();
        return 1;
    }
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'io.fabric'
apply plugin: 'kotlin-kapt'
apply plugin: 'realm-android'

android {
    compileSdkVersion 28
    def revision = getGitRevision()
    defaultConfig {
        applicationId "uk.co.cerihughes.mgm.android"
        minSdkVersion 21
        targetSdkVersion 28
        multiDexEnabled true
        versionCode revision
        versionName "1.1"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    testOptions {
        unitTests.returnDefaultValues = true
    }
}

dependencies {
    // Top level dependencies
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation project(path: ':remoteapi')

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "androidx.multidex:multidex:2.0.1"

    // Architecture dependencies
    implementation "androidx.lifecycle:lifecycle-extensions:$lifecycle_version"
    implementation "org.koin:koin-core:$koin_version"
    implementation "org.koin:koin-android-viewmodel:$koin_version"
    implementation "org.jetbrains.anko:anko-commons:0.10.0"

    // Android UI dependencies
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'com.google.android.material:material:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.vectordrawable:vectordrawable:1.1.0'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.exifinterface:exifinterface:1.1.0'

    // Other UI dependencies
    implementation 'com.squareup.picasso:picasso:2.71828'

    // Maps dependencies
    implementation 'com.google.android.gms:play-services-maps:17.0.0'

    // Networking dependencies
    implementation "org.threeten:threetenbp:$threeten_version"
    implementation "com.google.code.gson:gson:$gson_version"

    // Analytics dependencies
    implementation 'com.google.firebase:firebase-core:16.0.9'
    implementation 'com.crashlytics.sdk.android:crashlytics:2.10.1'

    testImplementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    testImplementation 'junit:junit:4.13'
    testImplementation "org.koin:koin-test:$koin_version"

    androidTestImplementation 'androidx.test:runner:1.2.0'
}

apply plugin: 'com.google.gms.google-services'