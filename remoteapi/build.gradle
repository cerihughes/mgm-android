buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
    }
    dependencies {
        classpath "org.openapitools:openapi-generator-gradle-plugin:7.0.0"
    }
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'org.openapi.generator'

def generatedOASDir = new File(buildDir, "generated/oas-codegen/")
def generatedOASSrcDir = new File(generatedOASDir, "src/main/kotlin/")

openApiGenerate {
    generatorName = "kotlin"
    inputSpec = "$rootDir/mgm-schema/v1/openapi.yaml"
    outputDir = generatedOASDir.getAbsolutePath()
    apiPackage = "uk.co.cerihughes.mgm.android.datasource.remote.generated.api"
    invokerPackage = "uk.co.cerihughes.mgm.android.datasource.remote.generated.invoker"
    modelPackage = "uk.co.cerihughes.mgm.android.datasource.remote.generated.model"
    configOptions = [
            "useGzipFeature" : "true",
            "useCoroutines": "true",
            "dateLibrary" : "java8"
    ]
}

preBuild.dependsOn tasks.openApiGenerate

android {
    compileSdk 34
    defaultConfig {
        minSdk 24
        targetSdk 34
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8
    }

    sourceSets {
        main {
            java {
                srcDirs += generatedOASSrcDir
            }
        }
    }
    namespace 'uk.co.cerihughes.mgm.android.remoteapi'
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // OAS generated
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.6.4"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    implementation "com.squareup.moshi:moshi-kotlin:$moshi_version"
    implementation "com.squareup.moshi:moshi-adapters:$moshi_version"
    implementation "com.squareup.okhttp3:okhttp:$okhttp_version"
}